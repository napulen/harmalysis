
    // harmalysis - a language for harmonic analysis and roman numerals
    // Copyright (C) 2020  Nestor Napoles Lopez

    // This program is free software: you can redistribute it and/or modify
    // it under the terms of the GNU General Public License as published by
    // the Free Software Foundation, either version 3 of the License, or
    // (at your option) any later version.

    // This program is distributed in the hope that it will be useful,
    // but WITHOUT ANY WARRANTY; without even the implied warranty of
    // MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    // GNU General Public License for more details.

    // You should have received a copy of the GNU General Public License
    // along with this program.  If not, see <https://www.gnu.org/licenses/>.


/////////////////////////////////
// Flats, sharps, and alterations
/////////////////////////////////
FLAT :         "-"  | "b"
DOUBLE_FLAT :  "--" | "bb"
SHARP :        "#"
DOUBLE_SHARP : "##" | "x"
_alteration : FLAT
            | DOUBLE_FLAT
            | SHARP
            | DOUBLE_SHARP

///////////////////////////////////////////
// Lower-case, upper-case letters, and keys
///////////////////////////////////////////
NOTE_LETTER_LOWERCASE : "a".."g"
NOTE_LETTER_UPPERCASE : "A".."G"

_MINOR_NATURAL :  "nat"
_MINOR_HARMONIC : "har"
_MINOR_MELODIC :  "mel"

KEY_AS_REFERENCE :   ":"
KEY_AS_ESTABLISHED : "=>:"

key_as_reference :   KEY_AS_REFERENCE
key_as_established : KEY_AS_ESTABLISHED

key_definition : NOTE_LETTER_UPPERCASE                                 -> major_key
               | NOTE_LETTER_UPPERCASE _alteration                     -> major_key_with_alteration
               | NOTE_LETTER_LOWERCASE                                 -> default_minor_key
               | NOTE_LETTER_LOWERCASE _alteration                     -> default_minor_key_with_alteration
               | NOTE_LETTER_LOWERCASE             "_" _MINOR_NATURAL  -> natural_minor_key
               | NOTE_LETTER_LOWERCASE _alteration "_" _MINOR_NATURAL  -> natural_minor_key_with_alteration
               | NOTE_LETTER_LOWERCASE             "_" _MINOR_HARMONIC -> harmonic_minor_key
               | NOTE_LETTER_LOWERCASE _alteration "_" _MINOR_HARMONIC -> harmonic_minor_key_with_alteration
               | NOTE_LETTER_LOWERCASE             "_" _MINOR_MELODIC  -> melodic_minor_key
               | NOTE_LETTER_LOWERCASE _alteration "_" _MINOR_MELODIC  -> melodic_minor_key_with_alteration

key : key_definition key_as_reference
    | key_definition key_as_established

////////////////
// Scale degrees
////////////////
TONIC_UPPERCASE :          "I"
SUPERTONIC_UPPERCASE :     "II"
MEDIANT_UPPERCASE :        "III"
SUBDOMINANT_UPPERCASE :    "IV"
DOMINANT_UPPERCASE :       "V"
SUBMEDIANT_UPPERCASE :     "VI"
SEVENTH_DEGREE_UPPERCASE : "VII"

TONIC_LOWERCASE :          "i"
SUPERTONIC_LOWERCASE :     "ii"
MEDIANT_LOWERCASE :        "iii"
SUBDOMINANT_LOWERCASE :    "iv"
DOMINANT_LOWERCASE :       "v"
SUBMEDIANT_LOWERCASE :     "vi"
SEVENTH_DEGREE_LOWERCASE : "vii"

_scale_degree_major : TONIC_UPPERCASE
                    | SUPERTONIC_UPPERCASE
                    | MEDIANT_UPPERCASE
                    | SUBDOMINANT_UPPERCASE
                    | DOMINANT_UPPERCASE
                    | SUBMEDIANT_UPPERCASE
                    | SEVENTH_DEGREE_UPPERCASE

_scale_degree_minor : TONIC_LOWERCASE
                    | SUPERTONIC_LOWERCASE
                    | MEDIANT_LOWERCASE
                    | SUBDOMINANT_LOWERCASE
                    | DOMINANT_LOWERCASE
                    | SUBMEDIANT_LOWERCASE
                    | SEVENTH_DEGREE_LOWERCASE

/////////////////////
// Fifth of the triad
/////////////////////
AUGMENTED_FIFTH :  "+"
DIMINISHED_FIFTH : "o"

/////////
// Triads
/////////
triad : _scale_degree_major                              -> major_triad
      | _alteration _scale_degree_major                  -> major_triad_with_alteration
      | _scale_degree_major             AUGMENTED_FIFTH  -> augmented_triad
      | _alteration _scale_degree_major AUGMENTED_FIFTH  -> augmented_triad_with_alteration
      | _scale_degree_minor                              -> minor_triad
      | _alteration _scale_degree_minor                  -> minor_triad_with_alteration
      | _scale_degree_minor             DIMINISHED_FIFTH -> diminished_triad
      | _alteration _scale_degree_minor DIMINISHED_FIFTH -> diminished_triad_with_alteration

//////////////////
// Added intervals
//////////////////
INTERVAL_UNISON :     "1"
INTERVAL_SECOND :     "2"
INTERVAL_THIRD :      "3"
INTERVAL_FOURTH :     "4"
INTERVAL_FIFTH :      "5"
INTERVAL_SIXTH :      "6"
INTERVAL_SEVENTH :    "7"
INTERVAL_OCTAVE :     "8"
INTERVAL_NINTH :      "9"
INTERVAL_TENTH :      "10"
INTERVAL_ELEVENTH :   "11"
INTERVAL_TWELFTH :    "12"
INTERVAL_THIRTEENTH : "13"
INTERVAL_FOURTEENTH : "14"
INTERVAL_FIFTEENTH :  "15"

MAJOR_INTERVAL :             "M"
MINOR_INTERVAL :             "m"
PERFECT_INTERVAL :           "P"
AUGMENTED_INTERVAL :         "A"
DIMINISHED_INTERVAL :        "D"
DOUBLE_AUGMENTED_INTERVAL :  "AA"
DOUBLE_DIMINISHED_INTERVAL : "DD"

_perfect_interval_quality : PERFECT_INTERVAL
                          | AUGMENTED_INTERVAL
                          | DIMINISHED_INTERVAL
                          | DOUBLE_AUGMENTED_INTERVAL
                          | DOUBLE_DIMINISHED_INTERVAL

_nonperfect_interval_quality : MAJOR_INTERVAL
                             | MINOR_INTERVAL
                             | AUGMENTED_INTERVAL
                             | DIMINISHED_INTERVAL
                             | DOUBLE_AUGMENTED_INTERVAL
                             | DOUBLE_DIMINISHED_INTERVAL

_any_interval_quality : MAJOR_INTERVAL
                      | MINOR_INTERVAL
                      | PERFECT_INTERVAL
                      | AUGMENTED_INTERVAL
                      | DIMINISHED_INTERVAL
                      | DOUBLE_AUGMENTED_INTERVAL
                      | DOUBLE_DIMINISHED_INTERVAL

added_seventh :                                 INTERVAL_SEVENTH    -> added_seventh_diatonic
              |    _nonperfect_interval_quality INTERVAL_SEVENTH    -> added_seventh_with_quality
added_ninth   :                                 INTERVAL_NINTH      -> added_ninth_diatonic
              |    _nonperfect_interval_quality INTERVAL_NINTH      -> added_ninth_with_quality
added_eleventh :                                INTERVAL_ELEVENTH   -> added_eleventh_diatonic
               |   _perfect_interval_quality    INTERVAL_ELEVENTH   -> added_eleventh_with_quality
added_thirteenth :                              INTERVAL_THIRTEENTH -> added_thirteenth_diatonic
                 | _nonperfect_interval_quality INTERVAL_THIRTEENTH -> added_thirteenth_with_quality

_tertian_interval : added_seventh
                  | added_ninth
                  | added_eleventh
                  | added_thirteenth

_descriptive_interval : _perfect_interval_quality    INTERVAL_UNISON
                      | _nonperfect_interval_quality INTERVAL_SECOND
                      | _nonperfect_interval_quality INTERVAL_THIRD
                      | _perfect_interval_quality    INTERVAL_FOURTH
                      | _perfect_interval_quality    INTERVAL_FIFTH
                      | _nonperfect_interval_quality INTERVAL_SIXTH
                      | _nonperfect_interval_quality INTERVAL_SEVENTH
                      | _perfect_interval_quality    INTERVAL_OCTAVE
                      | _nonperfect_interval_quality INTERVAL_NINTH
                      | _nonperfect_interval_quality INTERVAL_TENTH
                      | _perfect_interval_quality    INTERVAL_ELEVENTH
                      | _perfect_interval_quality    INTERVAL_TWELFTH
                      | _nonperfect_interval_quality INTERVAL_THIRTEENTH
                      | _nonperfect_interval_quality INTERVAL_FOURTEENTH
                      | _perfect_interval_quality    INTERVAL_FIFTEENTH

////////////////////////
// Inversions by numbers
////////////////////////
TRIAD_FIRST_INVERSION_NUMERIC :  "6"
TRIAD_SECOND_INVERSION_NUMERIC : "64"

triad_inversion_by_number : TRIAD_FIRST_INVERSION_NUMERIC
                          | TRIAD_SECOND_INVERSION_NUMERIC


// These are more than numeric inversions, they specify an added 7th
// AND a numeric inversion, within the same token
SEVENTHCHORD_FIRST_INVERSION_NUMERIC :  "65"
SEVENTHCHORD_SECOND_INVERSION_NUMERIC : "43"
SEVENTHCHORD_THIRD_INVERSION_NUMERIC:   "42" | "2"

seventhchord_inversion_by_number : SEVENTHCHORD_FIRST_INVERSION_NUMERIC
                                 | SEVENTHCHORD_SECOND_INVERSION_NUMERIC
                                 | SEVENTHCHORD_THIRD_INVERSION_NUMERIC

///////////////////////
// Inversions by letter
///////////////////////
TRIAD_INVERSION_LETTER :           "a".."c"
SEVENTHCHORD_INVERSION_LETTER :    "a".."d"
NINTHCHORD_INVERSION_LETTER :      "a".."e"
ELEVENTHCHORD_INVERSION_LETTER :   "a".."f"
THIRTEENTHCHORD_INVERSION_LETTER : "a".."g"

triad_inversion_by_letter :           TRIAD_INVERSION_LETTER           -> inversion_by_letter
seventhchord_inversion_by_letter :    SEVENTHCHORD_INVERSION_LETTER    -> inversion_by_letter
ninthchord_inversion_by_letter :      NINTHCHORD_INVERSION_LETTER      -> inversion_by_letter
eleventhchord_inversion_by_letter :   ELEVENTHCHORD_INVERSION_LETTER   -> inversion_by_letter
thirteenthchord_inversion_by_letter : THIRTEENTHCHORD_INVERSION_LETTER -> inversion_by_letter

////////////////////
// Missing intervals
////////////////////
MISSING_ROOT :     "1"
MISSING_THIRD :    "3"
MISSING_FIFTH :    "5"
MISSING_SEVENTH :  "7"
MISSING_NINTH :    "9"
MISSING_ELEVENTH : "11"

_MISSING_INTERVAL_SYMBOL : "x"

_missing_intervals_triad :           [_MISSING_INTERVAL_SYMBOL MISSING_ROOT] [_MISSING_INTERVAL_SYMBOL MISSING_THIRD] [_MISSING_INTERVAL_SYMBOL MISSING_FIFTH]
_missing_intervals_seventhchord :    _missing_intervals_triad
_missing_intervals_ninthchord :      _missing_intervals_seventhchord  [_MISSING_INTERVAL_SYMBOL MISSING_SEVENTH]
_missing_intervals_eleventhchord :   _missing_intervals_ninthchord    [_MISSING_INTERVAL_SYMBOL MISSING_NINTH]
_missing_intervals_thirteenthchord : _missing_intervals_eleventhchord [_MISSING_INTERVAL_SYMBOL MISSING_ELEVENTH]

missing_intervals_triad :           _missing_intervals_triad
missing_intervals_seventhchord :    _missing_intervals_seventhchord
missing_intervals_ninthchord :      _missing_intervals_ninthchord
missing_intervals_eleventhchord :   _missing_intervals_eleventhchord
missing_intervals_thirteenthchord : _missing_intervals_thirteenthchord

/////////////////
// Tertian chords
/////////////////
tertian_chord : triad                                                      missing_intervals_triad             -> tertian_triad
              | triad                  triad_inversion_by_number           missing_intervals_triad             -> tertian_triad_with_inversion_by_number
              | triad                  seventhchord_inversion_by_number    missing_intervals_seventhchord      -> tertian_seventh_with_inversion_by_number
              | triad                  triad_inversion_by_letter           missing_intervals_triad             -> tertian_triad_with_inversion_by_letter
              | triad added_seventh                                        missing_intervals_seventhchord      -> tertian_seventh
              | triad added_seventh    seventhchord_inversion_by_letter    missing_intervals_seventhchord      -> tertian_seventh_with_inversion_by_letter
              | triad added_ninth                                          missing_intervals_ninthchord        -> tertian_ninth
              | triad added_ninth      ninthchord_inversion_by_letter      missing_intervals_ninthchord        -> tertian_ninth_with_inversion_by_letter
              | triad added_eleventh                                       missing_intervals_eleventhchord     -> tertian_eleventh
              | triad added_eleventh   eleventhchord_inversion_by_letter   missing_intervals_eleventhchord     -> tertian_eleventh_with_inversion_by_letter
              | triad added_thirteenth                                     missing_intervals_thirteenthchord   -> tertian_thirteenth
              | triad added_thirteenth thirteenthchord_inversion_by_letter missing_intervals_thirteenthchord   -> tertian_thirteenth_with_inversion_by_letter

/////////////////
// Special chords
/////////////////
GERMAN :                  "Ger" | "Gn"
ITALIAN :                 "Lt"  | "It"
FRENCH :                  "Fr"
NEAPOLITAN :              "N"
TRISTAN :                 "Tr"
HALF_DIMINISHED_SEVENTH : "vii0"
CADENTIAL_SIX_FOUR :      "Cad"
COMMON_TONE_DIMINISHED :  "CTo"

german :                  GERMAN                  -> special_chord_name
italian :                 ITALIAN                 -> special_chord_name
french :                  FRENCH                  -> special_chord_name
neapolitan :              NEAPOLITAN              -> special_chord_name
tristan :                 TRISTAN                 -> special_chord_name
half_diminished_seventh : HALF_DIMINISHED_SEVENTH -> special_chord_name
cadential_six_four :      CADENTIAL_SIX_FOUR      -> special_chord_name
common_tone_diminished :  COMMON_TONE_DIMINISHED  -> special_chord_name

special_chord : german                                                         -> special_german
              | german                        seventhchord_inversion_by_letter -> special_german_with_inversion_by_letter
              | german                        seventhchord_inversion_by_number -> special_german_with_inversion_by_number
              | italian                                                        -> special_italian
              | italian                       triad_inversion_by_letter        -> special_italian_with_inversion_by_letter
              | italian                       triad_inversion_by_number        -> special_italian_with_inversion_by_number
              | french                                                         -> special_french
              | french                        seventhchord_inversion_by_letter -> special_french_with_inversion_by_letter
              | french                        seventhchord_inversion_by_number -> special_french_with_inversion_by_number
              | neapolitan                                                     -> special_neapolitan
              | neapolitan                    triad_inversion_by_letter        -> special_neapolitan_with_inversion_by_letter
              | neapolitan                    triad_inversion_by_number        -> special_neapolitan_with_inversion_by_number
              | tristan                                                        -> special_tristan
              | half_diminished_seventh ["7"]                                  -> special_halfdiminished
              | half_diminished_seventh ["7"] seventhchord_inversion_by_letter -> special_halfdiminished_with_inversion_by_letter
              | half_diminished_seventh       seventhchord_inversion_by_number -> special_halfdiminished_with_inversion_by_number
              | cadential_six_four ["64"]                                      -> special_cadential
              | common_tone_diminished ["7"]                                   -> special_commontone
              | common_tone_diminished ["7"]  seventhchord_inversion_by_letter -> special_commontone_with_inversion_by_letter
              | common_tone_diminished        seventhchord_inversion_by_number -> special_commontone_with_inversion_by_number

/////////////////////
// Descriptive chords
/////////////////////
pitch_class_root : NOTE_LETTER_LOWERCASE                 -> pitch_class
                 | NOTE_LETTER_LOWERCASE _alteration     -> pitch_class_with_alteration
                 | NOTE_LETTER_UPPERCASE                 -> pitch_class
                 | NOTE_LETTER_UPPERCASE _alteration     -> pitch_class_with_alteration

scale_degree_root : _scale_degree_major             -> scale_degree
                  | _scale_degree_minor             -> scale_degree
                  | _alteration _scale_degree_major -> scale_degree_with_alteration
                  | _alteration _scale_degree_minor -> scale_degree_with_alteration

_added_unison :                       [_perfect_interval_quality    INTERVAL_UNISON]
_added_second :     _added_unison     [_nonperfect_interval_quality INTERVAL_SECOND]
_added_third :      _added_second     [_nonperfect_interval_quality INTERVAL_THIRD]
_added_fourth :     _added_third      [_perfect_interval_quality    INTERVAL_FOURTH]
_added_fifth :      _added_fourth     [_perfect_interval_quality    INTERVAL_FIFTH]
_added_sixth :      _added_fifth      [_nonperfect_interval_quality INTERVAL_SIXTH]
_added_seventh :    _added_sixth      [_nonperfect_interval_quality INTERVAL_SEVENTH]
_added_octave :     _added_seventh    [_perfect_interval_quality    INTERVAL_OCTAVE]
_added_ninth :      _added_octave     [_nonperfect_interval_quality INTERVAL_NINTH]
_added_tenth :      _added_ninth      [_nonperfect_interval_quality INTERVAL_TENTH]
_added_eleventh :   _added_tenth      [_perfect_interval_quality    INTERVAL_ELEVENTH]
_added_twelfth :    _added_eleventh   [_perfect_interval_quality    INTERVAL_TWELFTH]
_added_thirteenth : _added_twelfth    [_nonperfect_interval_quality INTERVAL_THIRTEENTH]
_added_fourteenth : _added_thirteenth [_nonperfect_interval_quality INTERVAL_FOURTEENTH]
_added_fifteenth :  _added_fourteenth [_perfect_interval_quality    INTERVAL_FIFTEENTH]

descriptive_intervals : _added_fifteenth

descriptive_chord_by_letter : "?" pitch_class_root descriptive_intervals
descriptive_chord_by_degree : "?" scale_degree_root descriptive_intervals

////////////////
// Tonicizations
////////////////
by_scale_degree : "/" _scale_degree_major             -> by_scale_degree_major
                | "/" _alteration _scale_degree_major -> by_scale_degree_major_with_alteration
                | "/" _scale_degree_minor             -> by_scale_degree_minor
                | "/" _alteration _scale_degree_minor -> by_scale_degree_minor_with_alteration
                | "/" NEAPOLITAN                      -> by_scale_degree_neapolitan

tonicization : by_scale_degree+

///////////////////
// Harmalysis label
///////////////////
harmalysis :     tertian_chord                            -> harmalysis_tertian
           | key tertian_chord                            -> harmalysis_tertian_with_key
           |     tertian_chord               tonicization -> harmalysis_tertian_with_tonicization
           | key tertian_chord               tonicization -> harmalysis_tertian_with_key_and_tonicization
           |     special_chord                            -> harmalysis_special
           | key special_chord                            -> harmalysis_special_with_key
           |     special_chord               tonicization -> harmalysis_special_with_tonicization
           | key special_chord               tonicization -> harmalysis_special_with_key_and_tonicization
           |     descriptive_chord_by_degree              -> harmalysis_descriptive_by_degree
           | key descriptive_chord_by_degree              -> harmalysis_descriptive_by_degree_with_key
           |     descriptive_chord_by_letter              -> harmalysis_descriptive_by_letter

/////////////////////
// Implicit harmonies
/////////////////////
implicit : "(" harmalysis ")"

//////////////////////
// Alternate harmonies
//////////////////////
alternate : harmalysis "[" harmalysis "]"
// alternate_harmalysis : harmalysis ( "|" harmalysis )~1..3

////////
// Start
////////
?start : harmalysis | implicit | alternate